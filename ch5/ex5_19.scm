(use compat.sicp)

(define (make-new-machine)
    (let ((pc (make-register 'pc))
          (flag (make-register 'flag))
          (stack (make-stack))
          (trace-flag false)
          (breakpoints '())
          (count-from-label 0)
          (label '())
          (instruction-count 0)
          (the-instruction-sequence '()))
        (let ((the-ops
                (list (list 'initialize-stack
                            (lambda () (stack 'initialize)))
                      (list 'print-stack-statistics
                            (lambda () (stack 'print-statistics)))))
              (register-table
                (list (list 'pc pc) (list 'flag flag))))
            (define (allocate-register name)
                (if (assoc name register-table)
                    (error "Multiply defined register: " name)
                    (set! register-table
                        (cons (list name (make-register name))
                              register-table)))
                'register-allocated)
            (define (lookup-register name)
                (let ((val (assoc name register-table)))
                    (if val
                        (cadr val)
                        (error "Unknown register:" name))))
            (define (execute)
                (let ((insts (get-contents pc)))
                    (if (null? insts)
                        'done
                        ; break後のproceed時のcount-from-labelの値周りの実装が怪しいので注意
                        (let ((inst (car insts)))
                            (if (eq? (caar inst) 'label)
                                (begin
                                    (set! count-from-label 0)
                                    (set! label (cdar inst)))
                                (begin
                                    (set! count-from-label (+ count-from-label 1))
                                    (set! instruction-count (+ instruction-count 1))))
                            (if trace-flag
                                (print "label:" label ", instraction:" (car inst)))
                            (if (member (cons label count-from-label) breakpoints)
                                (print "BREAK: " (cons label count-from-label))
                                (begin
                                    ((instruction-execution-proc inst))
                                    (execute)))))))
            (define (print-and-init-instruction-count)
                (display (list "instruction count:" instruction-count))
                (newline)
                (set! instruction-count 0))
            (define (trace-on) (set! trace-flag true))
            (define (trace-off) (set! trace-flag false))
            (define (set-breakpoint label n)
                (if (not (member (cons label n) breakpoints))
                    (set! breakpoints (cons (cons label n) breakpoints))))
            (define (cancel-breakpoint label n)
                (if (member (cons label n) breakpoints)
                    (set! breakpoints (delete (cons label n) breakpoints))
                    (error "DEL-BREAKPOINT" label ", " n)))
            (define (del-all-breakpoint)
                (set! breakpoints '()))
            (define (dispatch message)
                (cond ((eq? message 'start)
                        (set-contents! pc the-instruction-sequence)
                        (execute))
                      ((eq? message 'install-instruction-sequence)
                        (lambda (seq) (set! the-instruction-sequence seq)))
                      ((eq? message 'allocate-register) allocate-register)
                      ((eq? message 'get-register) lookup-register)
                      ((eq? message 'install-operations)
                        (lambda (ops) (set! the-ops (append the-ops ops))))
                      ((eq? message 'stack) stack)
                      ((eq? message 'operations) the-ops)
                      ((eq? message 'print-and-init-instruction-count)
                        (print-and-init-instruction-count))
                      ((eq? message 'trace-on) (trace-on))
                      ((eq? message 'trace-off) (trace-off))
                      ((eq? message 'set-breakpoint) set-breakpoint)
                      ((eq? message 'cancel-breakpoint) cancel-breakpoint)
                      ((eq? message 'del-all-breakpoint) del-all-breakpoint)
                      ((eq? message 'proceed) execute)
                      (else (error "Unknown request -- MACHINE" message))))
            dispatch)))

(define (set-breakpoint machine label n)
    ((machine 'set-breakpoint) label n))

(define (cancel-breakpoint machine label n)
    ((machine 'cancel-breakpoint) label n))

(define (del-all-breakpoint machine)
    ((machine 'del-all-breakpoint)))

(define (proceed machine)
    ((machine 'proceed)))

(define (extract-labels text receive)
    (if (null? text)
        (receive '() '())
        (extract-labels
            (cdr text)
            (lambda (insts labels)
                (let ((next-inst (car text)))
                    (if (symbol? next-inst)
                        (let ((insts (cons (make-label-instruction next-inst) insts)))
                            (receive insts
                                     (cons (make-label-entry next-inst insts)
                                           labels)))
                        (receive (cons (make-instruction next-inst)
                                       insts)
                                 labels)))))))

(define (make-label-instruction label-name)
    (cons (cons 'label label-name) '()))

(define (make-execution-procedure inst labels machine
                                  pc flag stack ops)
    (cond ((eq? (car inst) 'assign)
            (make-assign inst machine labels ops pc))
          ((eq? (car inst) 'test)
            (make-test inst machine labels ops flag pc))
          ((eq? (car inst) 'branch)
            (make-branch inst machine labels flag pc))
          ((eq? (car inst) 'goto)
            (make-goto inst machine labels pc))
          ((eq? (car inst) 'save)
            (make-save inst machine stack pc))
          ((eq? (car inst) 'restore)
            (make-restore inst machine stack pc))
          ((eq? (car inst) 'perform)
            (make-perform inst machine labels ops pc))
          ((eq? (car inst) 'label)
            (lambda () (advance-pc pc)))
          (else
            (error "Unknown instruction type -- ASSEMBLE" inst))))
